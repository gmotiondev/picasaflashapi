<?xml version="1.0" encoding="utf-8"?>
<mx:WindowedApplication xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute" applicationComplete="init()"
	backgroundGradientAlphas="[1.0, 1.0]" backgroundGradientColors="[#EEEEEE, #FFFFFF]">
	<mx:Script>
		<![CDATA[
			private function init() : void
			{
				googleid.setFocus();
			}
			
			private function sendURLLoaderRequest() : void
			{
				var tURLVariables : URLVariables = new URLVariables();
					tURLVariables["accountType"] = "HOSTED_OR_GOOGLE";
					tURLVariables["Email"] = googleid.text;
					tURLVariables["Passwd"] = password.text; 
					tURLVariables["service"] = "lh2";
					tURLVariables["source"] = "Google_ClientLogin";
					
				var tRequest : URLRequest = new URLRequest();
					tRequest.url = "https://www.google.com/accounts/ClientLogin";
					tRequest.method = URLRequestMethod.POST;
					tRequest.data = tURLVariables;
					
					tRequest.requestHeaders.push(new URLRequestHeader("Content-type","application/x-www-form-urlencoded"));
					
				var tLoader : URLLoader = new URLLoader();
					tLoader.addEventListener(Event.COMPLETE, loader_result);
					tLoader.addEventListener(IOErrorEvent.IO_ERROR, loader_fault);
					tLoader.addEventListener(SecurityErrorEvent.SECURITY_ERROR, loader_fault);
					tLoader.addEventListener(HTTPStatusEvent.HTTP_STATUS, loader_status);
					
				try
				{
					tLoader.load(tRequest);
				} catch(e : Error)
				{
					throw new Error(e);
				}
	
			}
			
			public function loader_result(evt : Event) : void
			{
				var loader : URLLoader = URLLoader(evt.target); 
				debug.htmlText += "<font color=\"#00ff00\">Success</font>: " + loader.data + "\n";
			}
	
			public function loader_fault(evt : ErrorEvent) : void
			{
				debug.htmlText += "<font color=\"#ff0000\">Fault</font>: " + evt.text + "\n";
			}
		
			public function loader_status(evt : HTTPStatusEvent) : void
			{
				trace("loader_status: " + evt.status);
			}
		]]>
	</mx:Script>
	<mx:VBox left="10" right="10" top="10" bottom="10">
		<mx:HBox>
			<mx:Label text="GoogleID:" color="#444444"/>
			<mx:TextInput id="googleid" enter="sendURLLoaderRequest()"/>
		</mx:HBox>
		<mx:HBox>
			<mx:Label text="Password:" color="#444444"/>
			<mx:TextInput id="password" displayAsPassword="true" enter="sendURLLoaderRequest()"/>
			<mx:Button label="Authenticate" click="sendURLLoaderRequest()"/>
		</mx:HBox>
		<mx:TextArea width="100%" height="100%" backgroundColor="#444444" color="#FFFFFF" id="debug"/>
	</mx:VBox>
</mx:WindowedApplication>
